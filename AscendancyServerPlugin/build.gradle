buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "java-library"
apply plugin: 'com.github.johnrengelman.shadow'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    // Keep params
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    maven {
        name = 'aikar'
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
}

dependencies {

    //SpongeAPI
    provided ('org.spongepowered:spongeapi:5.1.0') {
        // Exclude sponge 5's configurate, we shade in configurate 4.0.0
        exclude group: 'ninja.leaping.configurate'
    }

    // Jetbrains Annotations
    compileOnly 'org.jetbrains:annotations:19.0.0'

    //AscendancyLib
    compile project(':AscendancyLib')

    // Guice assisted-inject
    compile('com.google.inject.extensions:guice-assistedinject:4.0') {
        exclude group: 'com.google.inject', module: 'guice'
    }

    // ACF
    compile 'co.aikar:acf-sponge:0.5.0-SNAPSHOT'

    // TaskChain
    compile 'co.aikar:taskchain-sponge:3.7.2'

    // Configurate 4.0.0
    implementation('org.spongepowered:configurate-yaml:4.0.0')

    // Adventure sponge
    implementation project(':adventure-backport:adventure-platform-spongeapi-5')

    // Adventure mini-message
    implementation ('net.kyori:adventure-text-minimessage:4.0.0-SNAPSHOT')
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    // TODO: Configure Forge build here
    forgeVersion = "1.10.2-12.18.3.2511"
    // TODO: Replace with your mappings version, e.g. stable_39 or snapshot_20180814
    mappings = project.properties.mcpVersion
}

assemble {
    dependsOn(project.shadowJar)
}

shadowJar {
    baseName = archivesBaseName
    version = "0.1-ALPHA"
    classifier = null
    relocate 'co.aikar.taskchain', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.taskchain'
    relocate 'co.aikar.commands', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.acf'
    relocate 'co.aikar.locales', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.locales'
    relocate 'com.google.inject', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.inject'
    // Relocate configurate to prevent conflicts with sponge (shouldn't be an issue for Sponge 5, may be for Sponge 7.)
    relocate 'com.spongepowered.configurate', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.configurate'
}
